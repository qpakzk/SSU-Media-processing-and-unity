---
layout: post
title: 프랙탈
description: >
  재귀 함수를 이용하여 프랙탈을 그려보도록 하겠다.
tags: [processing]
---

# 프랙탈

재귀 함수를 이용하여 프랙탈을 스캐치해 보도록 하겠다.

```java
float theta;
void setup() {
 size(600, 400);
}

void draw() {
  background(255);
  stroke(0);
  theta = 100;
  translate(width/2, height);
  line(0, 0, 0, -120);
  translate(0, -120);
  branch(100);
}

void branch(float h) {
  h *= 2.0/3.0;

  if(h > 2) {
    pushMatrix();
    rotate(theta);
    line(0, 0, 0, -h);
    translate(0, -h);
    branch(h);
    popMatrix();

    pushMatrix();
    rotate(-theta);
    line(0, 0, 0, -h);
    translate(0, -h);
    branch(h);
    popMatrix();
  }
}
```

결과는 다음과 같다.

![]({{ site.baseurl }}/assets/img/processing/fractal1.png)

규칙적이지 않은 벚꽃을 그려보도록 하겠다.

```java
void setup() {
  size(600, 600, P3D);
  smooth();
  noLoop();
}

void draw() {
  background(255);
  strokeWeight(10);

  for(int i = -300; i <= 300; i += 80) {
    pushMatrix();
    translate(width/2 + i, height - 20, i);
    branch(0);
    popMatrix();
  }
}

void branch(int depth) {
  if(depth < 12) {
    line(0, 0, 0, -height/10);
    {
       translate(0, -height/10);
       rotate(random(-0.1, 0.1));

       if(random(1.0) < 0.6) {
         rotate(0.3);
         scale(0.7);
         pushMatrix();
         branch(depth + 1);
         popMatrix();
         rotate(-0.6);
         pushMatrix();
         branch(depth + 1);
         popMatrix();
       }
       else {
         branch(depth);
       }
    }
  }
  else {
    noStroke();
    fill(#FFBFF9, 150);
    ellipse(0, 0, random(100, 300), random(100, 300));
    stroke(0);
  }
}

void mouseReleased() {
  redraw();
}
```

결과는 다음과 같다.

![]({{ site.baseurl }}/assets/img/processing/fractal2.png)
